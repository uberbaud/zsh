# @(#)[:2vLEy@AbLSC^^)rs6$y#: 2017/08/11 16:07:26 tw@csongor.lan]
# vim: ts=4 ft=zsh

setopt inc_append_history extended_history hist_ignore_dups
setopt hist_expire_dups_first hist_reduce_blanks hist_ignore_space
setopt beep notify interactive_comments re_match_pcre prompt_percent
setopt extended_glob rm_star_silent numeric_glob_sort
setopt bsd_echo chase_links ignore_eof
setopt combining_chars vi
unsetopt auto_cd bang_hist prompt_bang mult_ios
bindkey -a ':' .history-incremental-search-backward
bindkey -a ::: execute-named-cmd

typeset -U path
typeset -TU HELPPATH helppath
export HELPPATH

me=~/work/clients/me
Z=$ZDOTDIR
B=${ZDOTDIR}/bin
F=${ZDOTDIR}/functions
I=${ZDOTDIR}/include
L=${ZDOTDIR}/lib

export USRLIB=$XDG_DATA_HOME/c/lib
export USR_ZSHLIB=$ZDOTDIR/lib
# reorder (path is array-unique-special) or set in order path
# put things we install ahead of system bins in OUR path
[[ -d /usr/local/bin ]]&& path=( /usr/local/bin $path )
[[ -d $XDG_DATA_HOME/bin ]] && {
	LOCALBIN=$XDG_DATA_HOME/bin
	USRBIN=$LOCALBIN
	path=( $USRBIN $path )
}
[[ -d ~/bin/zsh ]] && {
	USRBIN=~/bin/zsh
	path=( $USRBIN $path )
}
[[ -n $USRBIN ]]&& export USRBIN
[[ -n $LOCALBIN ]]&& export LOCALBIN

# it's helpful for paths to be adjusted *BEFORE* sourcing `.include` â€¦
path=( $path /usr/games )
fpath=( $ZDOTDIR/usrfuncs.zwc $Z/completions $fpath )
# Autoload all functions we compile into usrfuncs.zwc
autoload -Uz ${${(f)"$(
		zcompile -t ${ZDOTDIR}/usrfuncs.zwc
	)"}[2,-1]#functions/}

. $ZDOTDIR/.include

# however, SYSLOCAL is defined in .include, so this happens after
helppath=(
		$ZDOTDIR/help
		$SYSLOCAL/share/zsh/$ZSH_VERSION/help
	)

# special handle run-help
(($+functions[run-help]))&& unfunction run-help
autoload -Uz run-help

# the following should have the unique trait, but don't by default
# Let's not create them, just mark them if they exist
#     see zshparam(1)
(( ${+chpwd_functions} ))			&& typeset -gaU chpwd_functions
(( ${+periodic_functions} ))		&& typeset -gaU periodic_functions
(( ${+precmd_functions} ))			&& typeset -gaU precmd_functions
(( ${+preexec_functions} ))			&& typeset -gaU preexec_functions
(( ${+zshaddhistory_functions} ))	&& typeset -gaU zshaddhistory_functions
(( ${+zshexit_functions} ))			&& typeset -gaU zshexit_functions
(( ${+path} ))						&& typeset -gaU path
(( ${+cdpath} ))					&& typeset -gaU cdpath
(( ${+fignore} ))					&& typeset -gaU fignore
(( ${+fpath} ))						&& typeset -gaU fpath
(( ${+mailpath} ))					&& typeset -gaU mailpath
(( ${+manpath} ))					&& typeset -gaU manpath
(( ${+module_path} ))				&& typeset -gaU module_path
(( ${+watch} ))						&& typeset -gaU watch
(( ${+zle_highlight} ))				&& typeset -gaU zle_highlight

# set errno, which otherwise would be set by the above test
true
