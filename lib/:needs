# @(#)[:;HpO7qiIgSKabPgE-abu: 2017/01/02 04:01:37 tw@csongor.lan]
# vim: filetype=zsh tabstop=4 textwidth=72 noexpandtab
# Tests for command existence and reports any not found.

emulate -L zsh
setopt local_options local_traps pipe_fail bsd_echo

typeset -a notfound=() cap_errs=()
typeset -A facilities=(
	:x11	'%BDISPLAY%b is not set. No %BX11%b?'
  )

function cap:x11 { (($+DISPLAY)); }

for need; do
	if (($+facilities[$need])); then
		cap$need || cap_errs+=( $facilities[$need] )
		whence $need >/dev/null && {
			-warn "%B${need:gs/%/%%}%b is a facility and a command."
		}
	elif ! whence $need >/dev/null; then
		notfound+=($need);
	fi
done

unset -f "cap${(k)^facilities}"

(($#notfound+$#cap_errs)) || return 0

-die $cap_errs "Can't find executables: %B$notfound%b"

# Copyright Â© 2017 by Tom Davis <tom@greyshirt.net>.
