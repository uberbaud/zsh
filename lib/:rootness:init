# @(#)[:k#a|CsDV1LV4)B7mGcR3: 2016/11/17 07:14:19 tw@csongor.lan]
# vim: filetype=zsh tabstop=4 textwidth=72 noexpandtab

emulate -L zsh

typeset -- expected='READY'
typeset -- LOG=~/log/simplesh
typeset -- RSH=~/local/bin/simplesh

[[ -x $RSH ]]|| {
	[[ -f $RSH ]] || -die "No such file %B${RSH:gs/%/%%}%b."
	-die "%B${RSH:gs/%/%%}%b is not executable."
}
touch $LOG || -die "Can't use %B${LOG:gs/%/%%}%b as log."

typeset -i triesleft=3
while ((triesleft--)); do
	coproc doas $RSH $expected 2>>$LOG
	read -rpt 5 || -warn 'Bad try.'
	[[ $REPLY == $expected ]] && break
done

(( triesleft == -1))&& -die '%BNot authorized%b.'

[[ $REPLY == $expected ]]|| {
	typeset -a msgbits=( 'Unexpected %Brootness%b condition.' )
	[[ -n $REPLY ]] && msgbits+=( "REPLY: %B${REPLY:gs/%/%%}%b" )
	printf "%s\n" $REPLY | sed -e 's/%[Bb]/|/g'  >>$LOG
	-die $msgbits
}

# Copyright (C) 2016 by Tom Davis <tom@greyshirt.net>.
