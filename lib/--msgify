# @(#)[:NoAgU5z4RPRX6IO)iQ~$: 2017/01/05 21:43:01 tw@csongor.lan]
# vim: filetype=zsh tabstop=4 textwidth=72 noexpandtab nowrap

emulate -L zsh

(( $#<2 )) && { print -rPu 2 '  %F{1}BAD PROGRAMMER%f (--msgify:1)'; exit 254; }

typeset -- prefix_1=$1
typeset -- prefix_N=$2
shift 2

[[ $# -eq 0 ]] && set '%F{251}[No message provided.]%f'

typeset -a Message=( $@ )

# Italic
typeset -- I=$'\e[3m'
typeset -- i=$'\e[23m'
Message=( ${Message//\%I/$I} )
Message=( ${Message//\%i/$i} )

# TTY
Message=( ${Message//\%T/%F\{2\}} )
Message=( ${Message//\%t/%f} )
# Special
Message=( ${Message//\%S/%F\{5\}} )
Message=( ${Message//\%s/%f} )
# Greyed out
Message=( ${Message//\%G/%F\{251\}} )
Message=( ${Message//\%g/%f} )

typeset -a CURPOS=()
[[ -x $LOCALBIN/get-row-col ]]&& CURPOS=( $($LOCALBIN/get-row-col) )
[[ ${CURPOS[2]:-1} -eq 1 ]]|| echo

print -rPu 2 $prefix_1 $Message[1]
shift Message
for line in $Message; do
	print -rPu 2 $prefix_N $line
done

# Copyright (C) 2016 by Tom Davis <tom@greyshirt.net>.
