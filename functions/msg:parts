# @(#)[:KqLrMA8zj}Wnr`e=zL,V: 2016/11/28 03:17:10 tw@csongor.lan]
# vim: filetype=zsh tabstop=4 textwidth=72 noexpandtab

emulate -L zsh
setopt local_options local_traps pipe_fail bsd_echo

typeset -- OPWD=$PWD
typeset -- work=$XDG_CACHE_HOME/mail

[[ -d $work ]]|| mkdir -p $work	|| -die "Could not create %S$work%s."
cd $work || -die "Could not %Tcd%t to %S$work%s."

setopt nullglob
typeset -a before=( * )

/usr/local/bin/mhstore $argv
typeset -a after=( * )
typeset -a new=()
typeset -- f='' H=''
for f in ${after:|before}; do
	[[ $f:e == txt && $( file -bi $f ) == text/html ]]&& {
		H=$f:r'.html'
		mv $f $H && f=$H
	  }
	new+=( $f )
done
open $new

cd $OPWD

# Copyright Â© 2016 by Tom Davis <tom@greyshirt.net>.
