# @(#)[:KqLrMA8zj}Wnr`e=zL,V: 2017/03/31 22:33:07 tw@csongor.lan]
# vim: filetype=zsh tabstop=4 textwidth=72 noexpandtab

emulate -L zsh
setopt local_options local_traps pipe_fail bsd_echo

typeset -- OPWD=$PWD
typeset -- work=$XDG_CACHE_HOME/mail

[[ -d $work ]]|| mkdir -p $work	|| -die "Could not create %S$work%s."
cd $work || -die "Could not %Tcd%t to %S$work%s."

local forceHTML=false
[[ $1 == -H ]]&& { forceHTML=true; shift; }

typeset -a before=( *(N) )

printf 'In \e[35m%s\e[39m\n' ${${work/#$HOME/~}:gs/%/%%}
/usr/local/bin/mhstore $argv
typeset -a after=( *(N) )
typeset -a new=()
typeset -- f='' H=''
for f in ${after:|before}; do
	if [[ $f:e == txt && $( file -bi $f ) == text/html ]]; then
		H=$f:r'.html'
		mv $f $H && f=$H
	elif [[ $f:e == html ]]; then
		:
	elif $forceHTML; then
		H=$f:r'.html'
		mv $f $H && f=$H
	fi
	new+=( $f )
done
open $new

cd $OPWD

# Copyright Â© 2016 by Tom Davis <tom@greyshirt.net>.
