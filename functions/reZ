#!/usr/bin/env zsh
# @(#)[:aB$rPFXh*PW+Ic*WhD*Y: 2016/11/28 05:15:11 tw@csongor.lan]
# vim: filetype=zsh tabstop=4 textwidth=72 noexpandtab

emulate -L zsh

(($#))&& {
	printf '  pulls changes and recompiles zsh.rc & common.zsh, etc\n'
	exit 0
  }

typeset -- OPWD=$PWD

[[ -n ${ZDOTDIR:-} ]]|| -die '%S$Z%s is not defined.'
cd $ZDOTDIR|| -die 'Could not %Tcd%t to %S$ZDOTDIR%s.'

alias log='print -Pu 2 " %F{4}>>>%f"'

typeset -- branch=$( git rev-parse --abbrev-ref HEAD 2>/dev/null )
typeset -- stash=''
[[ $branch == master ]]|| {
	[[ -n $(git status --porcelain) ]]&& {
		log " stashing changes on %B${branch:gs/%/%%}%b."
		stash=$( git stash )
	  }
	log 'Switching to %Bmaster%b branch.'
	git checkout master
  }
log 'Syncing with %Bremote%b.'
git pull

if [[ $stash == 'No local changes to save' ]]; then
	log 'Switching to %Blocal%b branch %F{5}'$HOST:r'%f.'
	git checkout $HOST:r

	log 'Merging changes from %Bmaster%b.'
	git merge master
else
	print -Pu 2 ' %F{1}WARNING%f stashed changes. Do your own merge!'
fi

typeset -a exen=(
	". $ZDOTDIR/bin/rezsh.zsh"
	$ZDOTDIR/lib/rezcompall.zsh
  )

log 'Recompiling everything.'
for x ($exen) $=x

cd $OPWD

# Copyright Â© 2016 by Tom Davis <tom@greyshirt.net>.
