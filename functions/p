# @(#)[:aV`l=qOi1z-j7-#72R#J: 2017/06/16 20:07:37 tw@csongor.lan]
# vim: filetype=zsh tabstop=4 textwidth=72 noexpandtab

emulate -L zsh
setopt local_options local_traps pipe_fail bsd_echo

cd ~/play
(($#))|| return 0	# no directory given so we're done
((1<$#))&&	{ print -Pu 2 '  %F{4}Usage%f: %F{2}p%f %Udir%u'; return 0; }

typeset -a possibles=()
if [[ -d $1 ]]; then
	possibles=( $1(N) )
elif [[ $1 == *\** ]]; then
	eval 'possibles=( $1(/N) )'
	(($#possibles))|| return 1	# glob symbols but no match, just quit
else
	eval 'possibles=( *$1*(/N) )'
fi

typeset -- choice='-'
if (($#possibles==1)); then
	choice=$possibles[1]
elif (($#possibles)); then
	choice=$( umenu $possibles )
else
	choice=$( umenu "<$1>" *(/N) )
	[[ $choice == "<$1>" ]]&& choice=$1
fi

[[ -n $choice ]]|| return
[[ -d $choice ]]|| mkdir -p $choice;

cd $choice

# Copyright Â© 2016 by Tom Davis <tom@greyshirt.net>.
