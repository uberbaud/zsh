# @(#)[:JP%W9J8Pwg*P@&D@<|aL: 2017/05/07 19:01:52 tw@csongor.lan]
# vim: filetype=zsh tabstop=4 textwidth=72 noexpandtab

# SYNOPSIS: handle mail from the "OpenBSD CVS change list" specially.

emulate -L zsh
setopt local_options local_traps pipe_fail bsd_echo

typeset -- P=${PAGER:-less}
needs =show fold awk $P cat

typeset -a filter=(
	# |>>> inbox:5          ← when there's more than one (1) message
	'/^>>> [a-z]+:[0-9]+$/' '{ N=$2; p=0; }'
	# |(Message inbox:5)    ← when there's only one (1) message
	'/^\(Message [a-z]+:[0-9]+\)$/' '{ sub("\\)",""); N=$2; p=0; }'
	# from start of log message excluding the header
	'/^Log [Mm]essage:/'
		'{'
			'p=1;'
			'B=0;'
			'printf( "%s%s\n", L, N);'
			'L = "\n";'
			'next;'
		'}'
	# until a blank line
	'p==0'						'{next;}'
	'/^$/'						'{B=1; next}'
	# |ok kettenis@[ dhill@]
	'/^[Oo][Kk]( [a-z]+@,?)+$/'	'{next;}'
	'B==1'				'{printf( "\n\n" ); B=0;}'
	# always
						'{printf( "%s ", $0 );}'
  )

typeset -- W=10
typeset -a refold=(
	'/^[a-z]+:[0-9]+$/' '{X=$0;next}'
	'{printf "%-'$W's%s\n", X, $0; X="";}'
  )

typeset -a OUTPUT=( ${(f)"$(\
	=show obsd						|
		awk "$filter"				|
		fold -s -w $((COLUMNS-W-2))	|
		awk "$refold"
	)"} )

[[ -t 1 ]]||			P=cat
(($#OUTPUT<LINES))&&	P=cat

for l ($OUTPUT) { echo $l }|$P


# Copyright © 2017 by Tom Davis <tom@greyshirt.net>.
