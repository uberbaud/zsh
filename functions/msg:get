#!/usr/bin/env zsh
# @(#)[:86XhN<3rNGtlDaQ#Mt%-: 2017/01/27 00:49:56 tw@csongor.lan]
# vim: filetype=zsh tabstop=4 textwidth=72 noexpandtab nowrap

emulate -L zsh
setopt local_options local_traps pipe_fail bsd_echo rematch_pcre

function quit { readonly X;X=FATAL 2>/dev/null; }

((${argv[(I)-h]}))&& {
	typeset -a Usage=(
		"  %F{4}Usage%f: %F{2}${${0##*/}:gs/%/%%}%f [%Uacct list%u]"
		'            Downloads messages for the given accounts, or'
		'            all accounts if none are given.'
		"         %F{2}${${0##*/}:gs/%/%%} -l%f"
		'            Lists available accounts.'
		"         %F{2}${${0##*/}:gs/%/%%} -h%f"
		'            Show this message.'
	  )
	print -Plu 2 $Usage
	quit
  }

needs $XDG_CONFIG_HOME/fetchmail/listAccts.zsh

((${argv[(I)-l]}))&& {
	$XDG_CONFIG_HOME/fetchmail/listAccts.zsh
	quit
  }

needs $XDG_CONFIG_HOME/fetchmail/accTofetch.zsh $SYSLOCAL/bin/fetchmail

typeset -- re_mb_typ1='^(\d+) (messages?) \((\d+) seen\) for (.*) at (.*)\.$'
typeset -- re_mb_typ2='^(\d+) (messages?) for (.*) at (.*)\.$'
typeset -- re_msg='^reading message (.*):(\d+) of (\d+) \('
typeset -- re_mb_empty='^fetchmail: No mail for (.*) at (.*)$'

typeset -- N W
typeset -- P='      %F{4}─%f'

print -Pu 2 '  %F{4}>>>%f Cleaning out %F{5}inbox%f.'
for delf ( $(mhpath)/,*(N) ) rm $delf

print -Pu 2 '  %F{4}>>>%f Generating %F{5}fetchmailrc%f.'
$XDG_CONFIG_HOME/fetchmail/accTofetch.zsh $@

print -Pu 2 '  %F{4}>>>%f Downloading remote messages...'
$SYSLOCAL/bin/fetchmail 2>&1 | while read -r resp; do
	if   [[ $resp =~ $re_msg ]]; then
		continue
	elif [[ $resp =~ $re_mb_typ1 ]]; then
		W=${match[4]:gs/%/%%}
		M=${match[2]:gs/%/%%}
		N=$((match[1]-match[3]))
		if ((N)); then
			print -Pu 2 "$P Getting %F{5}$N%f $M for %F{5}$W%f."
		else
			print -Pu 2 "$P No new messages for %F{5}$W%f."
		fi
	elif [[ $resp =~ $re_mb_typ2 ]]; then
		W=${match[3]:gs/%/%%}@${match[4]:gs/%/%%}
		M=${match[2]:gs/%/%%}
		N=$((match[1]))
		if ((N)); then
			print -Pu 2 "$P Getting %F{5}$N%f $M for %F{5}$W%f."
		else
			print -Pu 2 "$P No new messages for %F{5}$W%f."
		fi
	elif [[ $resp =~ $re_mb_empty ]]; then
		print -Pu 2 "$P No new messages for %F{5}${match[2]:gs/%/%%}%f."
	else
		warn $resp
	fi
done
# regenerate fetchmailrc with all accounts enabled
$XDG_CONFIG_HOME/fetchmail/accTofetch.zsh

typeset -i msgCount=$(egrep -c '^From ' /var/mail/tw)

(($msgCount))|| { warn "Nothing more to do, quitting."; return 0; }

print -Pnu 2 '  %F{4}>>>%f '
$SYSLOCAL/bin/inc -nochangecur


# Copyright © 2016 by Tom Davis <tom@greyshirt.net>.
