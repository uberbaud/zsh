# @(#)[:n8qQ21CarxQZ7WP5}uL?: 2017/06/26 01:04:17 tw@csongor.lan]
# vim: filetype=zsh tabstop=4 textwidth=72 noexpandtab
# Wrap various package facilities

emulate -L zsh
setopt local_options local_traps pipe_fail bsd_echo
local AsRoot='/usr/bin/doas'

needs $AsRoot cat die

(($#))|| set help

local osv=$( uname -r )
local cmd=$1
shift
local binpath="/usr/sbin"
local bin="$binpath/pkg_${cmd}"
local pkghome="$HOME/local/pkg-info/v${osv}"

local record=false
case $cmd in
	add) 		$AsRoot $bin -i $@; record=true							;;
	check)		$AsRoot $bin $@;										;;
	delete)		$AsRoot $bin $@; record=true							;;
	install)	die 'Did you mean %F{2}add%f or %F{2}install%Ued%u%f?';	;;
	installed)	$binpath/pkg_info -mz;									;;
	web|www)
		local opener==w3m
		[[ -n $opener ]]|| opener==open
		[[ -n $opener ]]|| needs w3m open
		local f='' u=''
		typeset -a h=()
		for f ($@) { h+=( $pkghome/html/$f* ); }
		for u ($~h) $opener $u
		;;
	grep|query)
		local ndx="$pkghome/v${osv}.ndx"
		local pkgblob="$( perl -ne "print if m/$1/;" $ndx )"
		echo $pkgblob | column -s $'\t' -t
		# save package names to global variable PKGs
		PKGs=()
		for p (${(f)pkgblob}) { PKGs+=( ${p%%$'\t'*} ); }
		local G=$'\e[0;38;5;251m' B=$'\e[0;38;5;111m' N=$'\e[0m'
		printf "\t${G}package names are in ${B}\$PKGs${G}.${N}\n"
		;;
	-h|--help|help)
		(($#))&& {
			cmd=$1; shift
			bin="${bin%_*}_$cmd"
			if [[ -x $bin ]]; then
				$bin -h
			else
				die "No help for %T${cmd:gs/%/%%}%t."
			fi
			return
		}
		local T=$'\e[32m' S=$'\e[35m' U=$'\e[4m' N=$'\e[0m'
		cat <<----
		  ${T}pkg${N} ${S}cmd${N} ${U}args...${N}
		      performs pkg_${S}cmd${N}, possibly using ${T}doas${N},
		      possibly logging the action.
		  ${T}pkg grep${N} ${S}regex${N}
		  ${T}pkg query${N} ${S}regex${N}
		      performs a ${B}perl5 regex${N} search on the index file
		      example: ${T}pkg query '\\b(?^i:lisp)\\b'${N}
		      performs a case insensitive word bounded search for
		          ${S}lisp${N}.
		  ${T}pkg installed${N}
		      lists manually installed packages in a format which can be
		      used by ${T}pkg_add -l${N} to do a best effort with flavors
		      but not hampered by version.
		  ${T}pkg web${N} ${S}globs …${N}
		  ${T}pkg www${N} ${S}globs …${N}
		      Shows the html information page using ${B}w3m${N}.
		---
		;;
	*)
		[[ -x $bin ]]|| die "Unknown command %S${cmd:gs/%/%%}%s."
		$bin $@
esac

$record && print $cmd $@ >> $HOME/hold/$osv/pkg.log

# Copyright © 2017 by Tom Davis <tom@greyshirt.net>.
