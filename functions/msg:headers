# @(#)[:Yi}e6Hh)3rU?Za!_g,N&: 2017/03/14 00:26:30 tw@csongor.lan]
# vim: filetype=zsh tabstop=4 textwidth=72 noexpandtab

# expect `msg:headers _file_ _field-key_ _filter-regex_`
(($#))||	die 'msg:headers: Missing %Bfile-name%b.'
(($#>3))&&	die 'msg:headers: Too many arguments.'

local TAB=$'\t' NL=$'\n' CRLF=$'\r\n' CR=$'\r' msg=$1 OLDPWD=$PWD
cd $(mhpath)

local M=${${"$(< $msg)"//$CRLF/$NL}//$CR/$NL}	# canonicalize newlines
M=${M%%$NL$NL*}
M=${M//$TAB/ }									# tabs to spaces
M=${M//$NL / }									# unwrap wrapped lines

(($#==1))&& { printf '%s\n' $M; return; }	# no field-key, so no filtering

local fld_regex="^$2$" matched=false

if (($#==2)); then
	function testfield {
		[[ ${1%%:*} =~ $fld_regex ]]||	return 1
		matched=true
		return 0
	}
elif (($#==3)); then
	local filter=$3
	function testfield {
		[[ ${1%%:*} =~ $fld_regex ]]||	return 1
		[[ ${1#*:} =~ $filter ]]||		return 1
		matched=true
		return 0
	}
else
	die 'Bad programmer!' 'Someone screwed up'
fi

for h (${(f)M}) { testfield $h && printf '%s: %s\n' $msg $h; }

cd $OLDPWD
$matched

# Copyright Â© 2017 by Tom Davis <tom@greyshirt.net>.
