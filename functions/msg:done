#!/usr/bin/env zsh
# @(#)[:G+eduCMfuZ4~cz%gTH!C: 2017/04/13 05:25:25 tw@csongor.lan]
# vim: filetype=zsh tabstop=4 textwidth=72 noexpandtab

emulate -L zsh
setopt local_options local_traps pipe_fail bsd_echo

folder +inbox

exec 3>$HOME/log/msg-done

local who=''
local M='\e[34m' P=' >>>' C='\e[0m' B='\e[1m' S='\e[35m'
function printSkipMsg { # {{{1
	printf "$M$P$B Skipping$S %s $C(no matching messages).\n" $1 >&2
} # }}}1
function printDoRefileMsg { # {{{1
	printf "$M$P$B Refiling messages for $S%s$C.\n" $1 >&2
} # }}}1
function X { #{{{1
	local filter=$1 who=$2 box=$3 msg=$4; shift 4

	msg:filter $filter $who $@ 2>&3
	if (($#MSGs)); then
		printDoRefileMsg $msg
		refile -unlink $MSGs +$box
	else
		printSkipMsg $msg
	fi
} # }}}1

#  Key    Pattern                        mailbox       message          $@
# ─────  ─────────────────────────────  ────────────  ────────────────  ──
X From   '@yt\.lan'                     yt.lan        '@yt.lan'         $@
X From   'root@csongor\.lan'            root@csongor  '@csongor'        $@
X To     'bgumm102@gmail\.com'          notes         'Notes to Self'   $@
X To     'source-changes@openbsd\.org'  obsd-cvs      'OpenBSD CVS'     $@



MSGs=( $(mhpath)/[1-9]*(Nn:t) )
(($#))&& MSGs=( ${MSGs:*argv} )
if (($#MSGs)); then
	print -Pu 2 ' %F{4}>>>%f %BRemoving%b everything else.'
	rmm $MSGs
else
	print -Pu 2 ' %F{4}>>>%f No messages to %Bremove%b.'
fi

typeset -a files2delete=( $XDG_CACHE_HOME/mail/*(.N) )
if (($#files2delete)); then
	typeset -- revert=false
	print -Pu 2 ' %F{4}>>>%f %BCleaning%b mail workshop.'
	yes-or-no "Delete the mail parts which maybe you're using" \
		&& rm $files2delete
fi

exec 3>&-

# Copyright © 2016 by Tom Davis <tom@greyshirt.net>.
