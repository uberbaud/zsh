# @(#)[:(2DMD-)}p4rmGSS$,!~e: 2017/01/24 03:24:22 tw@csongor.lan]
# vim: filetype=zsh tabstop=4 textwidth=72 noexpandtab
# safe replacement for pkill

emulate -L zsh
setopt local_options local_traps pipe_fail bsd_echo

needs dialog
(($#))|| die 'Expected process name pattern as an argument.'
(($#==1))|| die 'Too many arguments. Expected one (1).'

local p='' title='Kill, kill, kill!'
typeset -a cbitems=() 
pgrep -lf $1 | while read -r p; do
	cbitems+=(${p%% *} "${p#* }" on)
done

(($#cbitems))|| die 'No matching processes.'

dialog --keep-tite --stdout --checklist $title 0 0 0 $cbitems | read p

(($#p))|| return 1
typeset -- fmt='Really kill these %d processes? '
if (($#p==1)) fmt='Really kill this %d process? '

printf $fmt ${(w)#p}
read -sq || { echo; return 1; }

echo
kill $=p


# Copyright Â© 2017 by Tom Davis <tom@greyshirt.net>.
