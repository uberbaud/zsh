# $Id: git,v 1.5 2016/09/24 05:09:21 tw Exp tw $
# vim: filetype=zsh tabstop=4 textwidth=72 noexpandtab
emulate -L zsh

typeset -- gitCmd='XXX'
for a; do
	[[ $a =~ '^-' ]] && continue
	gitCmd=$a
	break
done

typeset -a checkForChanges=(
	add         branch      checkout    commit
	merge       mv          rebase      reset
	rm          tag
  )

if [[ -z ${checkForChanges[(r)$gitCmd]} ]]; then
	$SYSLOCAL/bin/git "$@"
	return $?
fi

typeset -- diffDir=$TMPDIR/git-study
[[ -d $diffDir ]]|| mkdir -p $diffDir	\
	|| die 'Could not create %S${diffDir:gs/%/%%}%s.'

typeset -a pwd=( ${(ps./.)PWD} )
typeset -- repoBase
while :; do
	repoBase=/${(pj./.)pwd}/.git
	[[ -d $repoBase ]] && break
	(( $#pwd ))|| die 'Not a %F{2}git%f repo.'
	shift -p pwd
done

function repoInfo { # {{{1
	print -r -- ".REPO $repoBase"
	print -r -- ".CMD ${(q)@}"
	find $repoBase -type f
} # }}}1
typeset -- marker=$( date +'%Y%m%d%H%M%S' )
typeset -- before=$diffDir/$marker.before
typeset -- after=$diffDir/$marker.after

repoInfo > $before
$SYSLOCAL/bin/git "$@"
typeset -i rc=$?
repoInfo > $after

# don't keep them if we didn't do anything
print -Pu 2 -- '  %F{4}>>>%f noting changes'
{ diff $before $after && rm $before $after }	\
	| cut -c 1-$COLUMNS							\
	| egrep '^[<>]'

return $rc

# Copyright (C) 2016 by Tom Davis <tom@greyshirt.net>.
